plugins {
    id 'java-library'
    id 'maven-publish'
}

apply plugin: 'org.openjfx.javafxplugin'

group = 'one.jpro'
version = "$JPRO_WEB_VERSION"

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "https://sandec.jfrog.io/artifactory/repo"
    }
}

dependencies {
    implementation "com.sandec.jpro:jpro-webapi:$JPRO_VERSION"
    implementation "com.auth0:java-jwt:$AUTH0_JAVAJWT_VERSION"
    implementation "com.auth0:jwks-rsa:$AUTH0_JWKSRSA_VERSION"
    implementation "org.jetbrains:annotations:$JETBRAINS_ANNOTATIONS_VERSION"
    api "jakarta.inject:jakarta.inject-api:$JAKARTA_INJECT_VERSION"
    api "org.slf4j:slf4j-api:$SLF4J_VERSION"
    api "org.json:json:$JSON_VERSION"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$JUNIT_VERSION"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$JUNIT_VERSION"
    testRuntimeOnly "ch.qos.logback:logback-classic:$LOGBACK_VERSION"
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

javafx {
    version = "$JAVAFX_VERSION"
    configurations = ["compileOnly", "testImplementation"]
    modules = ['javafx.graphics', 'javafx.controls']
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        showExceptions true
        showCauses true
        showStackTraces true
        exceptionFormat "full"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId project.group
            artifactId "jpro-auth"
            version project.version
            //artifact sourcesJar
        }
    }
    repositories {
        maven {
            name = "artifactory"
            url = "https://sandec.jfrog.io/artifactory/repo"
            credentials {
                username System.getenv("SANDEC_ARTIFACTORY_USERNAME")
                password System.getenv("SANDEC_ARTIFACTORY_PASSWORD")
            }
        }
    }
}
