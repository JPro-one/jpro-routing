System.setProperty("file.encoding", "UTF-8")

buildscript {
  repositories {
    jcenter()
    gradlePluginPortal()
    maven {
      url "https://sandec.jfrog.io/artifactory/repo"
    }
  }

  dependencies {
    classpath "com.sandec.jpro:jpro-plugin-gradle:$JPRO_VERSION"
    classpath "SANDEC:simplefx-plugin-gradle:$SIMPLEFX_VERSION"
    classpath 'org.openjfx:javafx-plugin:0.0.10'
  }
}

configure([project(':core'),project(':example')]) {
  repositories {
    jcenter()
    maven {
      url "https://sandec.jfrog.io/artifactory/repo"
    }
  }

  apply plugin: 'de.sandec.simplefx'
  apply plugin: 'org.openjfx.javafxplugin'

  javafx {
    version = "$JAVAFX_VERSION"
    modules = [ 'javafx.base', 'javafx.graphics', 'javafx.controls', 'javafx.fxml', 'javafx.media', 'javafx.web' ]
  }
}

configure([project(':core')]) {
  apply plugin: 'java'
  apply plugin: 'maven-publish'

  dependencies {
    compile "com.sandec.jpro:jpro-webapi:$JPRO_VERSION"
    compile "SANDEC:jnodes:$JNODES_VERSION"
    compile "de.sandec:JMemoryBuddy:0.5.0"
  }

  group = 'com.sandec.jpro'
  version = "$JPRO_WEB_VERSION"

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java
        groupId project.group
        artifactId ("jpro-web-" + project.name)
        version project.version
        //artifact sourcesJar
      }
    }
    repositories {
      maven {
        name = "artifactory"
        url = "https://sandec.jfrog.io/artifactory/repo"
        credentials {
          username System.getenv("SANDEC_ARTIFACTORY_USERNAME")
          password System.getenv("SANDEC_ARTIFACTORY_PASSWORD")
        }
      }
    }
  }

}



configure([project(':example')]) {
  apply plugin: 'com.sandec.jpro'
  mainClassName = "example.TestWebApplication"
  dependencies {
    compile project(':core')
    compile 'org.controlsfx:controlsfx:11.1.0'
  }

  compileJava {
    doFirst {
      options.compilerArgs = [
              '--module-path', classpath.asPath,
              '--add-modules', 'javafx.fxml,javafx.controls'
      ]
    }
  }

  run {
    doFirst {
      jvmArgs = [
              '--module-path', classpath.asPath,
              '--add-modules', 'javafx.fxml,javafx.controls'
      ]
    }
  }

  jpro {
    openingPath = "/"
  }
}
