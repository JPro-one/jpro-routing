System.setProperty("file.encoding", "UTF-8")

buildscript {
  repositories {
    jcenter()
    gradlePluginPortal()
    maven {
      url "https://sandec.jfrog.io/artifactory/repo"
    }
  }

  dependencies {
    classpath "com.sandec.jpro:jpro-plugin-gradle:$JPRO_VERSION"
    classpath "SANDEC:simplefx-plugin-gradle:$SIMPLEFX_VERSION"
    classpath 'org.openjfx:javafx-plugin:0.0.12'
  }
}

configure([project(':core'),project(':example')]) {
  repositories {
    jcenter()
    maven {
      url "https://sandec.jfrog.io/artifactory/repo"
    }
  }

  apply plugin: 'de.sandec.simplefx'
  apply plugin: 'org.openjfx.javafxplugin'

  compileScala {
    targetCompatibility = "1.8"
  }

  javafx {
    version = "$JAVAFX_VERSION"
    modules = [ 'javafx.base', 'javafx.graphics', 'javafx.controls', 'javafx.fxml', 'javafx.media', 'javafx.web' ]
  }

  test {
    useJUnitPlatform()

    testLogging.showStandardStreams = true
    testLogging.exceptionFormat org.gradle.api.tasks.testing.logging.TestExceptionFormat.FULL
    testLogging.showExceptions true
    testLogging.showCauses true
    testLogging.showStackTraces true
  }
}

configure([project(':core')]) {
  apply plugin: 'java'
  apply plugin: 'java-library'
  apply plugin: 'maven-publish'

  dependencies {
    api "com.sandec.jpro:jpro-webapi:$JPRO_VERSION"
    api "SANDEC:jnodes:$JNODES_VERSION"
    api "de.sandec:JMemoryBuddy:0.5.1"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
  }


  group = 'com.sandec.jpro'
  version = "$JPRO_WEB_VERSION"

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java
        groupId project.group
        artifactId ("jpro-routing-" + project.name)
        version project.version
        //artifact sourcesJar
      }
    }
    repositories {
      maven {
        name = "artifactory"
        url = "https://sandec.jfrog.io/artifactory/repo"
        credentials {
          username System.getenv("SANDEC_ARTIFACTORY_USERNAME")
          password System.getenv("SANDEC_ARTIFACTORY_PASSWORD")
        }
      }
    }
  }

}



configure([project(':example')]) {
  apply plugin: 'com.sandec.jpro'
  //mainClassName = "example.TestWebApplication"
  mainClassName = "example.TestExtensions"
  //mainClassName = "example.ColorTransitionApp"
  dependencies {
    implementation project(':core')
    implementation 'org.controlsfx:controlsfx:11.1.1'
    implementation "fr.brouillard.oss:cssfx:11.5.0"
  }

  jpro {
    openingPath = "/"
  }
}
